import com.axelor.gradle.support.ChangelogSupport

plugins {
  id 'com.axelor.app'
  id 'com.adarshr.test-logger' version '4.0.0'
}

apply from: './gradle/version.gradle'
apply plugin: ChangelogSupport

try {
  tasks.named('spotlessCheck')
} catch (UnknownTaskException ignored) {
  apply from: './gradle/style.gradle'
}

testlogger {
  theme = 'mocha'
}

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
}

group = 'com.axelor.addons'
description = 'Axelor Message Module'

axelor {
  title 'Axelor Message'
  description 'Axelor Message Module'
}

dependencies {
  implementation 'com.squareup.okhttp3:okhttp:5.1.0'
  implementation 'org.reflections:reflections:0.10.2'
  if (findProject(':modules:axelor-utils') != null) {
    implementation project(':modules:axelor-utils')
  } else {
    implementation 'com.axelor.addons:axelor-utils:4.0.0'
  }
}

changelog {
  version = baseVersion
  output.set(file('CHANGELOG.md'))
  inputPath.set(file('changelogs/unreleased'))
  types.set(['Feature', 'Change', 'Deprecate', 'Remove', 'Fix', 'Security'])
  header.set("${version.get()} (${new Date().format('yyyy-MM-dd')})")
}

//
// Publish
//

apply plugin: 'maven-publish'

ext {
  mavenUsername = project.findProperty('addonsMavenUsername')
  mavenPassword = project.findProperty('addonsMavenPassword')
}

publishing {
  repositories {
    maven {
      def repoPath = project.hasProperty('finalRelease')
          ? 'maven-releases'
          : 'maven-snapshots'
      name = 'maven'
      url = 'https://repository.axelor.com/nexus/repository/' + repoPath
      credentials {
        username = project.mavenUsername
        password = project.mavenPassword
      }
    }
  }
}

/*
* Jacoco
*/

apply plugin: 'jacoco'

jacoco {
  toolVersion = '0.8.13'
}

jacocoTestReport {
  reports {
    xml.required
  }
}

test {
  useJUnitPlatform()
  // report is always generated after tests run
  finalizedBy jacocoTestReport
  maxHeapSize = '1G'
}

jacocoTestReport {
  // tests are required to run before generating the report
  dependsOn test
}
