import com.axelor.gradle.support.ChangelogSupport

buildscript {
  ext.repos = {
    mavenCentral() {
      content { excludeGroupByRegex "com\\.axelor.*" }
    }
    maven {
      url 'https://plugins.gradle.org/m2/'
      content { excludeGroupByRegex "com\\.axelor.*" }
    }
    maven { url 'https://repository.axelor.com/nexus/repository/maven-public/' }
  }
  ext.aopVersion = '7.0.10'
  repositories repos
  buildscript.repositories repos
  dependencies {
    classpath "com.axelor:axelor-gradle:${aopVersion}"
    classpath "com.adarshr:gradle-test-logger-plugin:2.+"
  }
}

repositories repos
buildscript.repositories repos

apply from: "version.gradle"
apply plugin: "com.axelor.app"
apply plugin: 'com.adarshr.test-logger'
apply plugin: ChangelogSupport

try {
  tasks.named("spotlessCheck")
} catch (UnknownTaskException ignored) {
  apply from: './gradle/style.gradle'
}

testlogger {
  theme 'mocha'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

group = "com.axelor.addons"
description "Axelor Message Module"

axelor {
  title "Axelor Message"
  description "Axelor Message Module"
}

dependencies {
  implementation "com.squareup.okhttp3:okhttp:4.12.0"
  implementation 'org.reflections:reflections:0.10.2'
  if (findProject(":modules:axelor-utils") != null) {
    api project(":modules:axelor-utils")
  } else {
    implementation "com.axelor.addons:axelor-utils:3.0.1"
  }
  testImplementation 'org.awaitility:awaitility:4.2.0'
}

changelog {
  version = baseVersion
  output.set(file("CHANGELOG.md"))
  inputPath.set(file("changelogs/unreleased"))
  types.set(["Feature", "Change", "Deprecate", "Remove", "Fix", "Security"])
  header.set("${version.get()} (${new Date().format("yyyy-MM-dd")})")
}

//
// Publish
//

apply plugin: 'maven-publish'

ext {
  mavenUsername = project.findProperty("addonsMavenUsername")
  mavenPassword = project.findProperty("addonsMavenPassword")
}

publishing {
  repositories {
    maven {
      def repoPath = project.hasProperty("finalRelease")
          ? "maven-releases"
          : "maven-snapshots"
      name = "maven"
      url = "https://repository.axelor.com/nexus/repository/" + repoPath
      credentials {
        username = project.mavenUsername
        password = project.mavenPassword
      }
    }
  }
}

/*
* Jacoco
*/

apply plugin: 'jacoco'

jacoco {
  toolVersion = '0.8.11'
}

jacocoTestReport {
  reports {
    xml.enabled true
  }
}

test {
  useJUnitPlatform()
  // report is always generated after tests run
  finalizedBy jacocoTestReport
  maxHeapSize = '1G'
}

jacocoTestReport {
  // tests are required to run before generating the report
  dependsOn test
}
